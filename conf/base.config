/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    sanger-tol/sequencecomposition Nextflow base config file
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

process {

    errorStrategy = { task.exitStatus in ((130..145) + 104) ? 'retry' : 'finish' }
    maxRetries    = 2
    maxErrors     = '-1'

    // Most of the pipeline requires very little resources
    cpus   = 1
    // but still gradually increase the resources to allow the pipeline to self-heal
    memory = { check_max( 50.MB * task.attempt, 'memory' ) }
    time   = { check_max( 30.min * task.attempt, 'time' ) }

    // tabix needs pointers to the sequences in memory
    withName: '.*:.*:FASTA_WINDOWS:TABIX_TABIX_.*' {
        // 50 MB per 25,000 sequences
        memory = { check_max( 50.MB * task.attempt * Math.ceil(meta.n_sequences / 25000), 'memory' ) }
    }

    // fasta_windows takes more memory on larger genomes
    withName: 'FASTAWINDOWS' {
        // 1 CPU per 1 Gbp
        cpus   = { check_max( Math.ceil(fasta.size() / 1000000000), 'cpus' ) }
        // 250 MB per 100 Mbp
        memory = { check_max( 250.MB * task.attempt * Math.ceil(fasta.size() / 100000000), 'memory' ) }
    }

    withName:CUSTOM_DUMPSOFTWAREVERSIONS {
        cache = false
    }
}
